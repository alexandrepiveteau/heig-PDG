apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "com.google.gms.google-services"

def gitCommitDate = { ->
    def stdout = new ByteArrayOutputStream()
    try {
        exec {
            commandLine 'git', 'log', '-1', '--format=%ci'
            standardOutput = stdout
        }
        return date.substring(0, 10).replace('-', '.') + "-" + date.substring(11,16)
    } catch (Exception e) {
        return new Date().format('yyyy.MM.dd-HH:mm')
    }


}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "tupperdate.android"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName gitCommitDate()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/*'
        exclude 'META-INF/*.kotlin_module'
    }

    flavorDimensions "api"
    productFlavors {
        mock {
            dimension "api"
        }
        api {
            dimension "api"
        }
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerVersion "1.4.10"
        kotlinCompilerExtensionVersion "1.0.0-alpha07"
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs += ["-Xallow-jvm-ir-dependencies", "-Xskip-prerelease-check"]
            freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
        }
    }

    signingConfigs {
        prod {
            Properties prodSigning = new Properties()
            File prodFile = new File("mobile/signing.properties.prod")
            prodFile.withInputStream { prodSigning.load(it) }

            storeFile file(prodSigning.getProperty("keystore.file")) ?: "None"
            storePassword prodSigning.getProperty("keystore.password") ?: "None"
            keyAlias prodSigning.getProperty("alias.name") ?: "None"
            keyPassword prodSigning.getProperty("alias.password") ?: "None"
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.prod
        }
        release {
            signingConfig signingConfigs.prod
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

dependencies {

    // Backport some Java 8 libraries.
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.1.1"

    // Testing.
    testImplementation "androidx.test:core:1.3.0"
    testImplementation "junit:junit:4.13.1"
    testImplementation "org.mockito:mockito-core:2.28.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"

    // Project dependencies.
    implementation project(":common")

    // Kotlin dependencies.
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.4.0"

    // AndroidX and Jetpack
    implementation "androidx.activity:activity-ktx:1.2.0-beta01"
    implementation "androidx.fragment:fragment-ktx:1.3.0-beta01"
    implementation "androidx.browser:browser:1.2.0"
    implementation "com.google.android.material:material:1.2.1"

    // Firebase and Google Mobile Services
    implementation platform("com.google.firebase:firebase-bom:26.1.0")
    implementation "com.google.firebase:firebase-analytics"
    implementation "com.google.firebase:firebase-auth"
    implementation "com.google.firebase:firebase-firestore"
    implementation "com.google.firebase:firebase-messaging"

    // Client-side Ktor dependencies.
    implementation "io.ktor:ktor-client-cio:1.4.0"
    implementation "io.ktor:ktor-client-auth:1.4.0"
    implementation "io.ktor:ktor-client-json:1.4.0"
    implementation "io.ktor:ktor-client-serialization:1.4.0"

    // Jetpack Compose
    implementation "androidx.compose.animation:animation:1.0.0-alpha07"
    implementation "androidx.compose.ui:ui:1.0.0-alpha07"
    implementation "androidx.compose.material:material:1.0.0-alpha07"

    implementation "androidx.ui:ui-tooling:1.0.0-alpha07"

    // Jetpack Compose + Coil
    implementation "dev.chrisbanes.accompanist:accompanist-coil:0.3.3.1"
}
